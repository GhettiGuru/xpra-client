plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

ext {
    versionMajor = 1
    versionMinor = 0
    kotlin_version = '1.8.22'
}
assert ext.versionMinor < 100
version = "${ext.versionMajor}.${ext.versionMinor}"

android {
    namespace "io.github.xpra.client"
    compileSdkVersion 34

    defaultConfig {
        applicationId "io.github.xpra.client"
        minSdkVersion 25
        targetSdkVersion 34
        versionCode = versionMajor * 100 + versionMinor
        versionName version
        archivesBaseName = "${project.name}-$versionName"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

    buildFeatures {
        viewBinding true
    }

    signingConfigs {
        release {
            storeFile file(project.findProperty('MYAPP_RELEASE_STORE_FILE') ?: System.getenv('KEYSTORE_FILE'))
            storePassword project.findProperty('MYAPP_RELEASE_STORE_PASSWORD') ?: System.getenv('STORE_PASSWORD')
            keyAlias project.findProperty('MYAPP_RELEASE_KEY_ALIAS') ?: System.getenv('KEY_ALIAS')
            keyPassword project.findProperty('MYAPP_RELEASE_KEY_PASSWORD') ?: System.getenv('KEY_PASSWORD')
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

repositories {
    mavenLocal() // priority for locally published jars
    google()
    mavenCentral()
    // GitHub Packages for both artifacts
    maven {
        url = uri("https://maven.pkg.github.com/GhettiGuru/bee-encode")
        credentials {
            username = findProperty("GH_USERNAME") ?: System.getenv("GH_USERNAME") ?: ""
            password = findProperty("GH_TOKEN") ?: System.getenv("GH_TOKEN") ?: ""
        }
    }
    maven {
        url = uri("https://maven.pkg.github.com/GhettiGuru/rencode4j")
        credentials {
            username = findProperty("GH_USERNAME") ?: System.getenv("GH_USERNAME") ?: ""
            password = findProperty("GH_TOKEN") ?: System.getenv("GH_TOKEN") ?: ""
        }
    }
}

dependencies {
    // Project modules
    implementation project(':xpra-common')
    implementation project(':3rdparty:grafika')

    // Kotlin stdlib
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // GitHub Packages dependencies
    implementation 'com.github.GhettiGuru:bee-encode:0.3'
    implementation 'com.github.GhettiGuru:rencode4j:1.2.0'

    // AndroidX / lifecycle / coroutines
    def coroutines_version = '1.4.2'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$coroutines_version"

    implementation "androidx.core:core-ktx:1.6.0"
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.transition:transition:1.4.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    def lifecycle_version = "2.4.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    // Utilities
    implementation 'com.google.guava:guava:31.0-android'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.arcao:slf4j-timber:3.2'

    // Testing
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.robolectric:robolectric:4.7'

    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:core-ktx:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'org.mockito:mockito-android:3.11.2'
}
