plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

ext {
    versionMajor = 1
    versionMinor = 0
    kotlin_version = '1.9.23'
}
assert ext.versionMinor < 100
version = "${ext.versionMajor}.${ext.versionMinor}"

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace "com.github.jksiezni.xpra"
    compileSdk 34

    defaultConfig {
        applicationId "com.github.jksiezni.xpra"
        minSdk 25
        targetSdk 34
        versionCode versionMajor * 100 + versionMinor
        versionName version
        archivesBaseName = "${project.name}-$versionName"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE_PATH") ?: "keystore.jks")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")

            // Enable only v1 and v3 signing
            enableV1Signing true
            enableV2Signing false
            enableV3Signing true
            enableV4Signing false
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = false
        }
    }
}

repositories {
    google()
    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }

    maven {
        url = uri("https://maven.pkg.github.com/GhettiGuru/bee-encode")
        credentials {
            username = findProperty("GH_USERNAME") ?: System.getenv("GH_USERNAME") ?: ""
            password = findProperty("GH_TOKEN") ?: System.getenv("GH_TOKEN") ?: ""
        }
    }

    maven {
        url = uri("https://maven.pkg.github.com/GhettiGuru/rencode4j")
        credentials {
            username = findProperty("GH_USERNAME") ?: System.getenv("GH_USERNAME") ?: ""
            password = findProperty("GH_TOKEN") ?: System.getenv("GH_TOKEN") ?: ""
        }
    }
}

dependencies {
    implementation project(':xpra-common')
    implementation project(':3rdparty:grafika')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'com.github.GhettiGuru:bee-encode:0.3'
    implementation 'com.github.GhettiGuru:rencode4j:1.2.0'
    implementation 'com.arcao:slf4j-timber:3.1'

    def coroutines_version = '1.7.3'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$coroutines_version"

    def core_ktx_version = '1.12.0'
    def appcompat_version = '1.6.1'
    def preference_ktx_version = '1.2.1'
    def transition_version = '1.4.1'
    def material_version = '1.11.0'
    def coordinatorlayout_version = '1.2.0'
    def constraintlayout_version = '2.1.4'
    def recyclerview_version = '1.3.2'

    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.preference:preference-ktx:$preference_ktx_version"
    implementation "androidx.transition:transition:$transition_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$coordinatorlayout_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"

    def lifecycle_version = "2.7.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"

    implementation 'com.google.guava:guava:33.0.0-android'
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Add Profile Installer dependency to fix lint missing class error
    implementation "androidx.profileinstaller:profileinstaller:1.4.1"
}
